if (!isNeedFirstModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        if (!isNeedFirstModule.toBoolean()) {
            applicationId "com.lfh.first"
        }
        minSdkVersion  rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode  rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        flavorDimensions "default"
        consumerProguardFiles "consumer-rules.pro"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ AROUTER_MODULE_NAME : project.getName() ]
            }
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }


    sourceSets {
        main {
            if (!isNeedFirstModule.toBoolean()) {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/release/AndroidManifest.xml'
                java { exclude 'debug/**' }
            }
        }
    }

    productFlavors {
        dev {
            buildConfigField "String", "HTTP_HOST", rootProject.ext.debugurl["HTTP_HOST"]
            buildConfigField "String", "HTTP_H5_URL", rootProject.ext.debugurl["HTTP_H5_URL"]
        }

        online {
            buildConfigField "String", "HTTP_HOST", rootProject.ext.url["HTTP_HOST"]
            buildConfigField "String", "HTTP_H5_URL", rootProject.ext.url["HTTP_H5_URL"]
        }
    }

}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation rootProject.ext.dependencies["appcompat"]
    implementation rootProject.ext.dependencies["constraintlayout"]
    implementation project(path: ':frame')
//    implementation 'com.github.lifuhai:Utils:1.2'
    implementation rootProject.ext.dependencies["recyclerview"]

    implementation rootProject.ext.dependencies["arouter-api"]
    implementation rootProject.ext.dependencies["glide"]
    implementation 'com.github.gcacace:signature-pad:1.3.1'

    implementation rootProject.ext.dependencies["okhttp"]
    implementation rootProject.ext.dependencies["interceptor"]
    implementation rootProject.ext.dependencies["retrofit"]
    implementation rootProject.ext.dependencies["gson"]
    implementation rootProject.ext.dependencies["rxjava_adapter"]
    implementation rootProject.ext.dependencies["rxjava"]
    implementation rootProject.ext.dependencies["rxandroid"]
    annotationProcessor rootProject.ext.dependencies["arouter-compiler"]
    implementation rootProject.ext.dependencies["ultimatebarx"]

    // 底部导航控件。
    implementation 'com.jpeng:JPTabBar:1.4.0'
}